import os
import sys
import glob
import argparse

# ╭───────────────────────────────────────────────────────────────────────────╮
#   SETUP
# ╰───────────────────────────────────────────────────────────────────────────╯

datasets = set(
    # ["chlamydia", "mtbc-highquality", "rickettsia", "mycoplasma", "testdata", "mrsn", ""]
    ["mtbc-highquality", "smalltest_mtbc-highquality"]
)

# Assigning obtained values
DATASET_CHOSEN = config["DATA"]
if DATASET_CHOSEN not in datasets:
    sys.error(f"Dataset chosen is not available from the options: {datasets}")
    sys.exit(2)
MAIN_DIR = os.path.join(os.getcwd(), "data", DATASET_CHOSEN, "prefsuff-kmermatching")
os.makedirs(MAIN_DIR, exist_ok=True)
LOWERBOUND = config["LOWERBOUND"]
UPPERBOUND = config["UPPERBOUND"]
KMERSIZE = config["KMERSIZE"]
OFFSET = config["OFFSET"]


def main_dir(child):
    return os.path.join(MAIN_DIR, child)


sample_dirs = glob.glob(f"{MAIN_DIR}/../assemblies/*")
samples = [os.path.basename(acc) for acc in sample_dirs]

# -----------------------------------------------------------------------------

# ╭───────────────────────────────────────────────────────────────────────────╮
#   RULES
# ╰───────────────────────────────────────────────────────────────────────────╯
#
# RULE ALL
# -----------------------------------------------------------------------------


rule all:
    input:
        genbank=expand(
            main_dir("../assemblies/{sample}/annotation/{sample}.gbff"), sample=samples
        ),
        accessorygenes=main_dir(
            f"orthologue_calling/accgenes-lb{LOWERBOUND}-ub{UPPERBOUND}.tsv"
        ),
        finished=main_dir(".finished"),


# RULE: Make bwa index for every genome
# -----------------------------------------------------------------------------


rule makebwaidx:
    input:
        fastafiles=main_dir("../assemblies/{sample}/{sample}.fasta"),
    output:
        bwaindex=main_dir("../assemblies/{sample}/{sample}.fasta.bwt"),
    conda:
        "./envs/bwamem.yml"
    shell:
        """
        bwa index {input.fastafiles}
        """


# RULE: Annotate with bakta
# -----------------------------------------------------------------------------


rule bakta_annotation:
    input:
        fasta=main_dir("../assemblies/{sample}/{sample}.fasta"),
    output:
        tsv=main_dir("../assemblies/{sample}/annotation/{sample}.tsv"),
    conda:
        "./envs/bakta.yml"
    params:
        output_dir=main_dir("../assemblies/{sample}/annotation"),
        prefix="{sample}",
        bakta_db="/n/data1/hms/dbmi/baym/databases/bakta_dbv5/db",
    resources:
        mem_gb=15,
        disk_mb=20000,
    threads: 16
    shell:
        # note we aren't using --compliant
        """
        bakta  \
            --db {params.bakta_db}  \
            --output {params.output_dir}  \
            --prefix {params.prefix}  \
            --force  \
            --locus-tag {params.prefix} \
            {input.fasta}
        """


# RULE: Cluster with mmseqs2
# -----------------------------------------------------------------------------


rule mmseqs2_createdb:
    input:
        aminoacids_fasta=expand(
            main_dir("../assemblies/{sample}/annotation/{sample}.faa"), sample=samples
        ),
    output:
        seqdb=main_dir("orthologue_calling/sequenceDB"),
    conda:
        "./envs/mmseqs2.yml"
    resources:
        mem_gb=2,
        threads=12,
        time="0-01:00",
    shell:
        """
        mkdir -p $(dirname {output.seqdb})
        mmseqs createdb {input.aminoacids_fasta} --dbtype 1 --write-lookup 1 {output.seqdb}
        """


rule mmseqs2_clusterdb:
    input:
        seqdb=main_dir("orthologue_calling/sequenceDB"),
    output:
        clusterdb=main_dir("orthologue_calling/sequenceClusterDB.index"),
    params:
        cluster_mode="3",
        min_seq_id="0.7",
        tmp_dir=main_dir("orthologue_calling/tmp_dir"),
        outputbase=main_dir("orthologue_calling/sequenceClusterDB"),
    conda:
        "./envs/mmseqs2.yml"
    resources:
        mem_gb=10,
        threads=12,
        time="0-05:00",
    shell:
        """
        if [[ -f {params.outputbase}.dbtype ]]; then
            rm {params.outputbase}.dbtype
        fi
        mmseqs cluster \
            {input.seqdb}  \
            {params.outputbase}  \
            {params.tmp_dir}  \
            --cluster-mode {params.cluster_mode}  \
            --min-seq-id {params.min_seq_id}; \
        rm -r {params.tmp_dir}
        """


rule mmseqs2_createtsv:
    input:
        clusterdb=main_dir("orthologue_calling/sequenceClusterDB.index"),
        seqdb=main_dir("orthologue_calling/sequenceDB"),
    output:
        clustertsv=main_dir("orthologue_calling/cluster.tsv"),
    params:
        clusteroutput=main_dir("orthologue_calling/sequenceClusterDB"),
    conda:
        "./envs/mmseqs2.yml"
    resources:
        mem_gb=1,
        time="0-01:00",
        threads=8,
    shell:
        "mmseqs createtsv " "{input.seqdb} " "{input.seqdb} " "{params.clusteroutput} " "{output.clustertsv}"


# RULE: Find variably present proteins
# -----------------------------------------------------------------------------


rule variable_proteins:
    input:
        fastafiles=expand(
            main_dir("../assemblies/{sample}/{sample}.fasta"), sample=samples
        ),
        clustertsv=main_dir("orthologue_calling/cluster.tsv"),
    output:
        accessorygenes=main_dir(
            f"orthologue_calling/accgenes-lb{LOWERBOUND}-ub{UPPERBOUND}.tsv"
        ),
    conda:
        "./envs/udist.yml"
    params:
        lowerbound=f"{LOWERBOUND}",
        upperbound=f"{UPPERBOUND}",
    resources:
        mem_gb=30,
    shell:
        """
        python ./scripts/udist_variablegenes.py  \
            -f {input.fastafiles} \
            -i {input.clustertsv}  \
            -l {params.lowerbound}  \
            -u {params.upperbound}  \
            -o {output.accessorygenes}
        """


# CHECKPOINT: Create directory for every variable protein
# -----------------------------------------------------------------------------


checkpoint idsamples_woprotein:
    input:
        variable_proteinlist=main_dir(
            f"orthologue_calling/accgenes-lb{LOWERBOUND}-ub{UPPERBOUND}.tsv"
        ),
    output:
        directory(main_dir("variableproteins")),
    conda:
        "./envs/parallel.yml"
    threads: 50
    resources:
        mem_gb=40,
        time="0-05:00",
    shell:
        """
        ./scripts/idsamples_woprotein.sh \
                {input.variable_proteinlist} \
                {output} \
                0  
        """


rule setup_variableproteins:
    input:
        variable_proteinlist=main_dir(
            f"orthologue_calling/accgenes-lb{LOWERBOUND}-ub{UPPERBOUND}.tsv"
        ),
        protein_names=main_dir("variableproteins/{protein}/.varproteindir_created"),
    output:
        proteinrep_aa=main_dir("variableproteins/{protein}/REPRESENTATIVE/{protein}.aa"),
        proteinrep_nuc=main_dir(
            "variableproteins/{protein}/REPRESENTATIVE/{protein}.nuc"
        ),
        aafamily=main_dir(
            "variableproteins/{protein}/REPRESENTATIVE/{protein}-family.aa"
        ),
        nucfamily=main_dir(
            "variableproteins/{protein}/REPRESENTATIVE/{protein}-family.nuc"
        ),
        aln_aafamily=main_dir(
            "variableproteins/{protein}/REPRESENTATIVE/{protein}-family-aln.aa"
        ),
        aln_nucfamily=main_dir(
            "variableproteins/{protein}/REPRESENTATIVE/{protein}-family-aln.nuc"
        ),
    params:
        assembly_dir=main_dir("../assemblies"),
    resources:
        mem_gb=10,
    conda:
        "./envs/seqkit.yml"
    shell:
        """
        sample=$(echo "{wildcards.protein}" | sed 's/_[^_]*$//')
        outputdir=$(dirname {input.protein_names})
        mkdir -p $outputdir/REPRESENTATIVE
        mkdir -p $outputdir/fastmap

        seqkit grep -n -r -p "{wildcards.protein}[[:space:]]" \
                {params.assembly_dir}/$sample/annotation/"$sample".faa > \
                {output.proteinrep_aa}
        echo "{output.proteinrep_aa} made"
        cp {output.proteinrep_aa} {output.aafamily}

        seqkit grep -n -r -p "{wildcards.protein}[[:space:]]" \
                {params.assembly_dir}/$sample/annotation/"$sample".ffn > \
                {output.proteinrep_nuc}
        echo "{output.proteinrep_nuc} made"
        cp {output.proteinrep_nuc} {output.nucfamily}

        for proteinfamilymember in $(grep {wildcards.protein} "{input.variable_proteinlist}" | datamash transpose | sed 1d | grep -v '0' -w); do
            newsample=$(echo "$proteinfamilymember" | sed 's/_[^_]*$//')
            echo "Adding this $newsample to the family"
            seqkit grep -n -r -p "$proteinfamilymember[[:space:]]" \
                    {params.assembly_dir}/$newsample/annotation/"$newsample".faa >> \
                    {output.aafamily}

            seqkit grep -n -r -p "$proteinfamilymember[[:space:]]" \
                    {params.assembly_dir}/$newsample/annotation/"$newsample".ffn >> \
                    {output.nucfamily}
        done
        echo "{output.aafamily} made"
        echo "{output.nucfamily} made"

        muscle -align {output.aafamily} -output {output.aln_aafamily}
        echo "{output.aln_aafamily} made"

        mafft --auto {output.nucfamily} > {output.aln_nucfamily}
        echo "{output.aln_nucfamily} made"
        """


rule get_prefsuffkmers:
    input:
        varproteindir_sentinel=main_dir(
            "variableproteins/{protein}/.varproteindir_created"
        ),
    output:
        prefsuffkmers=main_dir(
            f"variableproteins/{{protein}}/REPRESENTATIVE/k{KMERSIZE}_offset{OFFSET}-prefsuffixkmers.fasta"
        ),
    params:
        kmersize=KMERSIZE,
        offset=OFFSET,
    conda:
        "./envs/biopython.yml"
    shell:
        """
        sample=$(echo "{wildcards.protein}" | sed 's/_[^_]*$//')
        tsv="$(dirname {input})/../../../assemblies/$sample/annotation/$sample.tsv"
        fasta="$(dirname {input})/../../../assemblies/$sample/$sample.fasta"
        python ./scripts/prefsuff_kmerextraction.py $tsv $fasta {wildcards.protein} {params.kmersize} {params.offset} {output} 
        """


rule fastmap_kmers:
    input:
        prefsuffkmers=main_dir(
            f"variableproteins/{{protein}}/REPRESENTATIVE/k{KMERSIZE}_offset{OFFSET}-prefsuffixkmers.fasta"
        ),
        genome_file=main_dir("../assemblies/{sample}/{sample}.fasta"),
    output:
        bwafastmap_out=main_dir(
            f"variableproteins/{{protein}}/fastmap/fastmap_rawoutput-{{sample}}"
        ),
    params:
        kmersize=KMERSIZE,
    conda:
        "./envs/bwamem.yml"
    shell:
        """
        bwa fastmap -w 99999 -l {params.kmersize} {input.genome_file} {input.prefsuffkmers} > {output.bwafastmap_out}
        """


rule process_fastmap_kmers:
    input:
        bwafastmap_out=main_dir(
            f"variableproteins/{{protein}}/fastmap/fastmap_rawoutput-{{genomes}}"
        ),
    output:
        dist_calc=main_dir(
            f"variableproteins/{{protein}}/fastmap/fastmap_processed-{{genomes}}"
        ),
    conda:
        "./envs/pandas.yml"
    shell:
        """
        python ./scripts/process_fastmapout.py {input} > {output}
        """


rule collate_processed_fastmap_pergene:
    input:
        lambda wildcards: expand(
            main_dir(
                f"variableproteins/{{protein}}/fastmap/fastmap_processed-{{genomes}}"
            ),
            protein=wildcards.protein,
            genomes=samples,
        ),
    output:
        main_dir(f"variableproteins/{{protein}}/collated_fastmap"),
    shell:
        """
        echo -e "GenomeName\\tkmers-matched\\tprefix-kmer\\tsuffix-kmer\\tcase" > {output}
        cat {input} >> {output} 
        """


def aggregate_prefsuffixkmers(wildcards):
    # protein_names = list_variableproteins(wildcards)
    checkpoint_output = checkpoints.idsamples_woprotein.get(**wildcards).output[0]
    protein_dirs = glob.glob(checkpoint_output + "/*")
    protein_names = [os.path.basename(p) for p in protein_dirs]
    # protein_names = [os.path.basename(p) for p in protein_dirs if "fastmap" not in p and "REPRESENTATIVE" not in p and "varproteindir" not in p and "variableproteins" not in p]
    return expand(
        main_dir(
            f"variableproteins/{proteins}/REPRESENTATIVE/k{KMERSIZE}_offset{OFFSET}-prefsuffixkmers.fasta"
        ),
        proteins=protein_names,
    )


def aggregate_familyaln(wildcards):
    checkpoint_output = checkpoints.idsamples_woprotein.get(**wildcards).output[0]
    protein_dirs = glob.glob(checkpoint_output + "/*")
    protein_names = [os.path.basename(p) for p in protein_dirs]
    return expand(
        main_dir(
            "variableproteins/{proteins}/REPRESENTATIVE/{proteins}-family-aln.nuc"
        ),
        proteins=protein_names,
    )


def aggregate_collatedprocessed_fastmap(wildcards):
    checkpoint_output = checkpoints.idsamples_woprotein.get(**wildcards).output[0]
    protein_dirs = glob.glob(checkpoint_output + "/*")
    protein_names = [os.path.basename(p) for p in protein_dirs]
    return expand(
        main_dir(f"variableproteins/{proteins}/collated_fastmap"),
        proteins=protein_names,
    )


rule finished:
    input:
        aggregate_prefsuffixkmers,
        aggregate_familyaln,
        aggregate_collatedprocessed_fastmap,
    output:
        main_dir(".finished"),
    shell:
        "touch {output}; sleep 5"
